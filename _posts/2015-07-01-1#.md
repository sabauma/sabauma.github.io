---
layout: post
title: "Compiling to 1#"
authors: "Spenser Bauman"
date: 2015-07-03
---

What is 1#?
-----------

1# is a [Turing Tarpit](https://en.wikipedia.org/wiki/Turing_tarpit) language
made to be both extremely minimal and
[homoiconic](https://en.wikipedia.org/wiki/Homoiconicity).
1# is used by the Indiana University logic group to teach recursion theory.

The language itself consists of sequences of 1s and #s which can be used to
denote the 5 instructions for a text register machine.
The text register machine consists of a program counter and an infinite set of
registers capable of holding the characters '1 and '#'.

So, what does the instruction set look like?
All instructions are of the form \\( 1^n \\#^m \\), where \\( 1^n \\) denotes
n repetitions of the character '1'.

1. \\( 1^n \\# \\) : Append a '1' to the end of register n
2. \\( 1^n \\#\\# \\) : Append a '#' to end of register n
3. \\( 1^n \\# \\# \\# \\) : Move _forward_ n instructions
4. \\( 1^n \\# \\# \\# \\# \\) : Move _backward_ n instructions
5. \\( 1^n \\# \\# \\# \\# \\# \\) : Case on the contents of register n
    - If register n is empty, move forward one instruction
    - If the first character of register n is a '1', delete that character and
      advance two instructions
    - If the first character of register n is a '#', delete that character and
      advance three instructions

See, very minimal. So minimal, in fact, that 1# is a huge pain to use for
any program consisting of more than a few instruction.
So, having already signed up for a class which uses 1#, what can be done to make our
lives easier?

The DSL Approach
----------------

Rather than come up with a whole new language that can be converted to 1#,
we'll leverage an existing language and programmatically generate our 1#
programs.
This is hardly novel, [even for 1#](http://www.indiana.edu/~iulg/trm/), but this
time we choose to embed our 1# generation language in Haskell.
The choice of Haskell is quite intentional, the safety gained from using Haskell
will save debugging time down the line; we have enough problem dealing with 1#,
so lets avoid introducing bugs via our meta-language as well.

The rest of this post will go into the details of the Haskell DSL. The full
source can be found at:

<center>
[https://github.com/sabauma/OneHash](https://github.com/sabauma/OneHash)
</center>

What is a 1# Program?
=====================

From the perspective of Haskell, what is a 1# program?
Well, a literal formulation from the definition above will be somewhat
cumbersome to deal with, particularly ensuring that we never mess up any of the
relative jump instructions.
Instead, we choose to represent jump locations as labels and convert them to
relative jumps later.

```haskell
newtype Reg = Reg { regIndex :: Int }
  deriving Show

data Instruction
  = Add1 Reg
  | AddH Reg
  | Label String
  | Jump String
  | Case Reg
  | Nop
  | Comment String
  deriving Show

type Instructions = [Instruction]
```
Now, our program is simply a stream of instructions, with labels placed at
points in the code which may be targeted by absolute jump instructions.
Note the additional `Nop` and `Comment` constructors. `Nop` is a boring
implementation detail, while `Comment` is used to insert notes comments into the
generated 1# program (someone has to grade the result).

The actual conversion process to 1# is straightforward, so lets worry about
making some useful coding constructs out of this simple language definition.
Manually constructing a stream of instructions is a slightly lower level of
abstraction than we are shooting for.

Lets consider the program which moves the contents of register 2 to register 1.

```
11#####     ;; case on R2
111111###   ;;   R2 is empty, so go forward 6 (end)
111###      ;;   Got a 1, go forward 3
1##         ;;   Got a #, write a # to R1
1111####    ;; Go back to the beginning
1#          ;; Write a 1 to R1
111111####  ;; Go back to the beginning
```

We can encode this manually as, but the result isn't much prettier.

```haskell
program = [ Label "START"
          , Case (Reg 2)
          , Jump "END"
          , Jump "GOT ONE"
          , AddH (Reg 1)
          , Label "GOT ONE"
          , Add1 (Reg 1)
          , Jump "START"
          , Label "END" ]
```

Instead, we would like to write something resembling the logical structure of
the 1# program, without manually constructing everything.
In particular

1. Manually managing register numbers is error prone, we want something like
   variables.
2. Manually inserting labels at the correct location is tedious and spelling
   mistakes are easy.
3. We want something that can be composed easily to make larger programs without
   worrying about register use and label naming.

Instead, we would like to express our move function as a loop over the contents
of the input register, taking the appropriate action on each iteration.
Perhaps something like this

```haskell
move :: Reg -> Reg -> OneHash ()
move source target = loop' source (add1 target) (addh target)
```

where we delegate the register and label management to the `OneHash` type.
Well, `OneHash` has to keep enough state to handle register management and
produce a stream of instructions, so leveraging the `State` and `Writer` monads
is a good place to start.

```haskell
data OneHashState = OneHashState
  { counter :: !Int
  , temps   :: [Reg]
  } deriving (Show)

newtype OneHash a = OneHash { unHash :: StateT OneHashState (Writer [Instruction]) a }
  deriving (Functor, Monad)
```

Now, we have a monad stack which manages a `OneHashState` and an output
instruction stream.
A `OneHashState` consists of a counter for label name generation and a list of
unused registers.
Defining the `OneHash` type as a Monad allows the use of `do`-notation to
compose our 1# programs.

Now, we can define some nice helper functions.

```haskell
add1, addh :: Reg -> OneHash ()
add1 r = tell [Add1 r]
addh r = tell [AddH r]

newLabelName :: String -> OneHash Label
newLabelName nm = do
  st <- get
  put $ st { counter = counter st + 1 }
  return $ MkL $ "LABEL_" ++ nm ++ show (counter st)

emitLabel :: Label -> OneHash ()
emitLabel l = tell [Label $ name l]

jump :: Label -> OneHash ()
jump l = tell [Jump $ name l]

namedLabel :: String -> OneHash (OneHash ())
namedLabel nm = do
  l <- newLabelName nm
  emitLabel l
  return $ jump l

-- Generates a label which can be jumped to using the returned OneHash action.
label :: OneHash (OneHash ())
label = namedLabel ""
```

The only thing surprising here is `label` action, which generates a new label at
the current point in the instruction stream and returns a new action which will
jump to that location.
This saves the user from manually handling labels and invoking `jump` (only
`label` and `namedLabel` are exported by the defining module).
Because labels are emitted at the same location where their names are generated,
how do we produce a forward jump?
We don't want the user to have to write something like this

```haskell
f = do
  l <- newLabelName ""
  jump l
  {- code -}
  emitLabel l
  {- more code #-}
```

where the user must generate a new label name and emit it separately, this is
messy and error prone.
Someone could easily use the wrong label, or accidentally emit the same label
twice.

Instead, we define loops using the monadic fix point operation `mfix`, which
allows us to define `f` as

```haskell
f = mfix $ \l -> do
  l
  {- code -}
  label

{- Or using RecursiveDo -}

f = mdo
  l
  {- code -}
  l <- label
```

Much less complicated.
Now that we have the ability to perform forward jumps, we can define a case
operation similar to the one in 1#, but without manually creating/jumping to
code blocks and without fall through behaviour.
The definition is fairly concise

```haskell
cases :: Reg -> OneHash () -> OneHash () -> OneHash () -> OneHash ()
cases r c1 c2 c3 = void $ mfix $ \ ~(j1, j2, jend) -> do
  tell [Case r] >> j1 >> j2 >> c3 >> jend
  liftA3 (,,) (label <* c1 <* jend) (label <* c2) label
```

The code generates three labels and uses the case operation as a jump table.
The jump table is immediately followed by code generated by the three `OneHash`
actions passed to cases.

An additional, useful operation which allows jumping to the top and bottom of
a code segment is `withLabels`

```haskell
withLabels :: (OneHash () -> OneHash () -> OneHash ()) -> OneHash ()
withLabels body = void $ mfix $ \ ~(start, end) ->
  (,) <$> label <* body start end <*> label
```

The function passed to `withLabels` is supplied with jump operations which
target labels placed just before and immediately after the code produced by
the given function when invoked.
`withLabels` provides a simple way to produce looping abstractions, where the
input to `withLabels` expects a `continue` and `break` action.

```haskell
loop :: Reg                                      -- Loop control register
     -> (OneHash () -> OneHash () -> OneHash ()) -- Got a 1
     -> (OneHash () -> OneHash () -> OneHash ()) -- Got a #
     -> OneHash ()
loop r one hash = withLabels $ \cont break ->
  cases r break (one cont break >> cont) (hash cont break >> cont)

while :: Reg -> OneHash () -> OneHash ()
while r m = withLabels $ \start end -> cases r end noop noop >> m >> start

k² :: a -> b -> c -> a
k² = const . const

loop' :: Reg -> OneHash () -> OneHash () -> OneHash ()
loop' r = loop r `on` k²
```

This gets us the nice looping construct defined above.

What about registers?
=====================

Alright, so looping is cool, but if we are programmatically generating a lot of
code, how can we be sure we are not erroneously reusing registers?


